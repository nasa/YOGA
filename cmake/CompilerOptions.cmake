function(add_warnings target)
    get_compiler_warning_options(default_warnings)
    separate_arguments(default_warnings)
    target_compile_options(${target} PRIVATE
            ${default_warnings}
            ${ARGN}
            )
endfunction()

function(get_developer_compiler_warning_options warnings)
    if ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "MSVC")
        set(host_compiler_warnings "/WX /W4")
    elseif ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "GNU")
        set(host_compiler_warnings "-Wall -Werror -Wunused -Wuninitialized -Woverloaded-virtual -pedantic-errors -Wno-unused-parameter -Wno-cast-function-type -Wno-deprecated-copy -Wnon-virtual-dtor -Wno-unknown-pragmas -Wno-ignored-qualifiers -Wno-deprecated-declarations -Wno-unused-but-set-parameter")
        if (${CMAKE_CXX_COMPILER_VERSION} VERSION_LESS "10")
            # Due to Kokkos enum comparsions that gcc flags as a warning
            set(host_compiler_warnings "${host_compiler_warnings} -Wextra")
        endif ()
    elseif ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "Clang")
        set(host_compiler_warnings "-Wall -Wextra -Werror -Wunused -Wuninitialized -pedantic-errors -Wno-unused-parameter -Wno-return-type-c-linkage -Wno-deprecated-copy -Wno-tautological-overlap-compare -Wno-gnu-zero-variadic-macro-arguments -Wno-unknown-pragmas -Wno-dynamic-class-memaccess -Wno-deprecated-declarations -Wno-unknown-cuda-version")
    elseif ("${CMAKE_CXX_COMPILER_ID}" STREQUAL "AppleClang")
        set(host_compiler_warnings "-Wall -Wextra -Werror -Wno-unused-parameter -Wno-return-type-c-linkage -Wno-deprecated -Wno-unused-command-line-argument -Wno-range-loop-analysis -Wno-unknown-pragmas -Wno-deprecated-declarations")
    endif ()
    if (CMAKE_CXX_COMPILER MATCHES nvcc_wrapper)
        set(${warnings} "-Xcudafe \"--display_error_number --diag-suppress=1305 --diag-suppress=177 --diag-suppress=68 --diag-suppress=186 --diag-suppress=20012 --Werror all-warnings\"" PARENT_SCOPE)
    else ()
        set(${warnings} ${host_compiler_warnings} PARENT_SCOPE)
    endif ()
endfunction()

function(get_production_compiler_warning_options warnings)
    if (CMAKE_CXX_COMPILER MATCHES nvcc_wrapper)
        set(${warnings} "-Xcudafe \"--display_error_number --diag-suppress=1305 --diag-suppress=177 --diag-suppress=68 --diag-suppress=186 --diag-suppress=20012 \"" PARENT_SCOPE)
    endif ()
endfunction()

function(get_compiler_warning_options warnings)
    if (${RING_DEVELOPER_COMPILE_MODE})
        get_developer_compiler_warning_options(compiler_warnings)
    else()
        get_production_compiler_warning_options(compiler_warnings)
    endif ()
    set(${warnings} ${compiler_warnings} PARENT_SCOPE)
endfunction()
